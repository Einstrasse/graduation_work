#******************************************************************
#
# Copyright 2015 Samsung Electronics All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

##
# ResourceBroker Unit Test build script
##
# SConscript file for Local PKI google tests
from tools.scons.RunTest import run_test

gtest_env = SConscript('#extlibs/gtest/SConscript')
lib_env = gtest_env.Clone()

if lib_env.get('RELEASE'):
    lib_env.AppendUnique(CCFLAGS=['-Os'])
else:
    lib_env.AppendUnique(CCFLAGS=['-g'])

SConscript('#service/third_party_libs.scons', 'lib_env')

######################################################################
#unit test setting
######################################################################
broker_test_env = lib_env.Clone()
src_dir = broker_test_env.get('SRC_DIR')
target_os = broker_test_env.get('TARGET_OS')

######################################################################
# Build flags
######################################################################

broker_test_env.AppendUnique(CPPPATH=[
    '../include',
    '../../../include',
    '../../common/primitiveResource/include',
    '../../common/expiryTimer/include',
    '../../common/expiryTimer/src',
    '../../common/utils/include',
    '#/resource/c_common',
    '#/resource/c_common/oic_malloc/include',
    '#/resource/c_common/oic_string/include',
    '#/resource/csdk/include',
    '#/resource/csdk/stack/include',
    '#/resource/include',
    '#/resource/oc_logger/include'
])

broker_test_env.PrependUnique(CPPPATH=['#/extlibs/hippomocks/hippomocks'])

broker_test_env.PrependUnique(LIBS=['coap'])
broker_test_env.AppendUnique(LIBS=[
'connectivity_abstraction',
'oc_logger',
'octbstack',
'oc',
'rcs_client',
'rcs_common',
])

compiler = broker_test_env.get('CXX')
if 'g++' in compiler:
    broker_test_env.AppendUnique(
        CXXFLAGS=['-O2', '-g', '-Wall', '-fmessage-length=0', '-std=c++0x'])

if broker_test_env.get('SECURED') == '1':
    broker_test_env.AppendUnique(LIBS=['mbedtls', 'mbedx509', 'mbedcrypto'])

######################################################################
# Build Test
######################################################################

broker_test_src = broker_test_env.Glob('./*.cpp')
broker_test = broker_test_env.Program('broker_test', broker_test_src)
Alias("broker_test", broker_test)
broker_test_env.AppendTarget('broker_test')

if broker_test_env.get('TEST') == '1':
    if target_os in ['linux']:
        run_test(
            broker_test_env,
            #'service_resource-encapsulation_src_resourceBroker_unittest_broker_test.memcheck',
            '',  # TODO: fix this test on linux and enable previous line
            'service/resource-encapsulation/src/resourceBroker/unittest/broker_test',
            broker_test)
