#******************************************************************
#
# Copyright 2017 Intel Mobile Communications GmbH All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
##
# Nest Plugin build script
##

import os
import os.path

Import('env')

target_os = env.get('TARGET_OS')
src_dir = env.get('SRC_DIR')
bridging_path = os.path.join(src_dir, 'bridging')

nest_env = env.Clone()

print("Reading Nest Plugin script")


def maskFlags(flags):
    flags = [flags.replace('-Wl,--no-undefined', '') for flags in flags]
    return flags


######################################################################
# Build flags
######################################################################

nest_env.PrependUnique(CPPPATH=[
    '#/resource/c_common/oic_malloc/include',
    '#/resource/c_common/oic_string/include',
    '#/resource/c_common',
    '#/resource/oc_logger/include',
    '#/resource/csdk/logger/include',
    '#/resource/csdk/include',
    '#/resource/csdk/stack/include',
    '#/resource/include',
    '#/extlibs/cjson',
    '#/extlibs/tinycbor/src',
    '#/extlibs/rapidjson/rapidjson/include/rapidjson'
])
nest_env.AppendUnique(CPPPATH=[
    '#/bridging/include',
    '#/bridging/plugins/nest_plugin/nest_objects',
])

if target_os not in ['windows']:
    nest_env.AppendUnique(CPPDEFINES=['WITH_POSIX'])

if target_os in ['darwin', 'ios']:
    nest_env.AppendUnique(CPPDEFINES=['_DARWIN_C_SOURCE'])

if 'g++' in nest_env.get('CXX'):
    nest_env.AppendUnique(
        CXXFLAGS=['-std=c++0x', '-Wall', '-Wextra', '-Werror'])

if nest_env.get('LOGGING'):
    nest_env.AppendUnique(CPPDEFINES=['TB_LOG'])

libmpm = nest_env.get('BUILD_DIR') + 'libmpmcommon.a'
nest_env['LINKFLAGS'] = maskFlags(env['LINKFLAGS'])
nest_env.AppendUnique(LINKFLAGS=[
    '-Wl,--allow-shlib-undefined',
    '-Wl,--whole-archive', libmpm,
    '-Wl,-no-whole-archive',
])

nest_env.PrependUnique(LIBS=[
    'm',
    'octbstack',
    'ocsrm',
    'connectivity_abstraction',
    'coap',
    'curl',
])

#####################################################################
# Source files and Target(s)
######################################################################

nest_src = [
    'nestResource.cpp',
    'nest_objects/nest.cpp',
    'nest_objects/nestThermostat.cpp',
]

nest_env.AppendUnique(NEST_SRC=nest_src)
nestlib = nest_env.SharedLibrary('nestplugin', nest_env.get('NEST_SRC'))
nest_env.Depends(nestlib, libmpm)
nest_env.InstallTarget(nestlib, 'nestplugin')
nest_env.UserInstallTargetLib(nestlib, 'nestplugin')
